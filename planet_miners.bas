5 REM Original TRS-80 version of Planet Miners
10  CLS : PRINT : PRINT : PRINT "THE PLANET MINERS!"
20  PRINT "EXPANSION INTO THE SOLAR SYSTEM."
30  PRINT : PRINT "COPYRIGHT 1980 BY AVALON HILL MICROCOMPUTER GAMES"
80  DATA "SUN",0,0,0,"MERCURY",57.9,7.1381306E-2,3,"VENUS",108.2,2.7972187E-2,0
110  DATA "EARTH",149.6,1.7202895E-2,1,"MARS",227.9,9.150476E-3,2,"CERES",413.6,3.7373224E-3,2
140  DATA "JUPITER",778,1.4502525E-3,7,"SATURN",1427,5.840002E-4,6,"URANUS",2870,2.047598E-4,1
170  DATA "NEPTUNE",4496,1.043941E-4,1,"PLUTO",5910,6.94505E-5,1
200 NUM_PLANETS = 10:NUM_SHIPS = 5:NUM_PLAYERS = 4:NUM_PATROLS = 12:F1% = 10
280  DIM planetName(NUM_PLANETS),orbitRadius(NUM_PLANETS),planetAngle(NUM_PLANETS),angularVelocity(NUM_PLANETS),Q(NUM_PLANETS),T(NUM_PLANETS),resources(NUM_PLANETS,NUM_PLAYERS)
350 MAX_PLAYER = NUM_PLAYERS - 1:MAX_PATROLS = NUM_PATROLS - 1:MAX_SHIP = NUM_SHIPS - 1
360  DIM S0%(MAX_PLAYER,MAX_SHIP),S1%(MAX_PLAYER,MAX_SHIP),S2%(MAX_PLAYER,MAX_SHIP),S3%(MAX_PLAYER,MAX_SHIP),S4%(MAX_PATROLS),S5%(MAX_PATROLS),S6%(MAX_PATROLS)
420 M9% = 15:M8% = 45: RANDOM :SCREEN_ADDR = 15360:CHARS_LINE = 64
460 S9% = 160:S8% = 75:D4% = 40
490  DIM B1$(MAX_PLAYER),S%(MAX_PLAYER),P(MAX_PLAYER),O%(MAX_PLAYER),H%(MAX_PLAYER)
540 rem Initialize Data
560 D0% = 1:D3% = D4% +  INT (D4% *  RND (0) / 4): RESTORE : FOR I = 0 TO NUM_PLANETS
570  READ planetName(I),orbitRadius(I),angularVelocity(I),resourceSeed
590  FOR J = 1 TO NUM_PLAYERS:resources(I,J) = 0: NEXT :planetAngle(I) = 6.28 *  RND (0)
600 resources(I,0) =  INT (resourceSeed * ( RND (0) +  RND (0) +  RND (0) +  RND (0))): NEXT 
610  FOR I = 0 TO NUM_PLAYERS - 1: FOR J = 0 TO NUM_SHIPS - 1:S0%(I,J) = 0:S3%(I,J) = 0
620 S1%(I,J) = 3:S2%(I,J) = 3: NEXT : FOR K = 1 TO F1%
630 J =  INT ( RND (0) * NUM_SHIPS):S0%(I,J) = S0%(I,J) + 1: NEXT 
640 resourceSeed =  - 1: FOR J = 0 TO NUM_SHIPS - 1: IF S0%(I,J) < resourceSeed THEN 660
650 resourceSeed = S0%(I,J):H%(I) = J
660  NEXT 
670  NEXT 
680  FOR J = 0 TO NUM_PLAYERS - 1: READ B1$(J): NEXT 
690  DATA "SIEGLER","AICHI","REGGIANE","LYSANDER"
750  FOR J = 0 TO NUM_PATROLS - 1:P% = 1 +  INT (NUM_PLANETS *  RND (0)):S6%(J) = 0
760 S4%(J) = P%:S5%(J) = P%: NEXT 
770  FOR K = 1 TO NUM_PLAYERS: CLS : PRINT "READY TO INITIALIZE FAMILY";K:A$ = ""
780  INPUT "WILL A PERSON PLAY THIS FAMILY (Y/N)";A$
790  IF (A$ = "N") OR (A$ = "") THEN 910
800  PRINT : PRINT : INPUT "YOUR FAMILY NAME";B1$(K - 1)
820  PRINT : PRINT : PRINT "WELCOME TO THE YEAR 2050. YOU ARE THE LEADER OF THE"
821  PRINT "FABULOUSLY WEALTHY AND POLITICALLY POWERFUL ";B1$(K - 1);"S."
840  PRINT : PRINT "EARTH GOVERNMENT IS ABOUT TO OPEN THE SOLAR SYSTEM"
841  PRINT "FOR MINING CONCESSIONS. YOU WILL BE RACING AGAINST";NUM_PLAYERS - 1
850  PRINT "OTHER FAMILIES, EACH HAVING";NUM_SHIPS;"SPACE SHIPS. EARTH WILL"
861  PRINT "BE MAINTAINING";NUM_PATROLS;"PATROL SPACE SHIPS, JUST TO KEEP"
870  PRINT "EVERYTHING FRIENDLY. GOOD LUCK!"
890  PRINT : INPUT "PRESS ENTER TO CONTINUE";A$
900 S%(K - 1) = 1: GOTO 920
910 S%(K - 1) = 0
920 O%(K - 1) = K:P%(K - 1) = 0: NEXT 
930  CLS : PRINT "WORKING..."
940 S% = 0: FOR P = 1 TO NUM_PLANETS:S% = S% + resources(P,0): NEXT : IF S% = 0 OR (D0% > D3% AND  RND (0) > .9) THEN 4520
950 T% = 0: FOR J = 1 TO NUM_PLANETS: GOSUB 2340:T(J) = resources(J,0) - 2 * F%
960  FOR K = 1 TO NUM_PLAYERS:T(J) = T(J) + resources(J,K): NEXT 
970  IF T(J) < 0 THEN T(J) = 0
980  IF J = 3 THEN T(J) = T(J) + 10
990 T% = T% + T(J): NEXT 
1000 Q(0) = 0: FOR P = 1 TO NUM_PLANETS:Q(P) = Q(P - 1) + resources(P,0) / S%: NEXT :Q(0) = S%
1010 T(0) = 0: FOR P = 1 TO NUM_PLANETS:T(P) = T(P - 1) + T(P) / T%: NEXT :T(0) = T%
1020  FOR J = 0 TO NUM_PLAYERS - 1:K =  INT ( RND (0) * NUM_PLAYERS):S% = O%(J):O%(J) = O%(K):O%(K) = S%: NEXT 
1030  FOR A = 0 TO NUM_PLAYERS - 1:P9% = O%(A)
1040  IF S%(P9% - 1) = 0 THEN 1080
1050  CLS 
1060  PRINT "COMMANDS FOR ";B1$(P9% - 1);" FOR DAY";D0%;":"
1070  GOSUB 1250: IF C% = 11 THEN 1090
1071  GOTO 1070
1080  GOSUB 3410
1090  NEXT 
1100  GOSUB 3930: GOTO 930
1120  PRINT "SELECT FROM THE FOLLOWING MENU:"
1130 : REM  rem 
1140  PRINT "1.  DISPLAY LARGE SOLAR SYSTEM"
1150  PRINT "2.  DISPLAY SMALL SOLAR SYSTEM"
1160  PRINT "3.  DISPLAY TRAVEL TIMES"
1170  PRINT "4.  DISPLAY MINING INFORMATION"
1180  PRINT "5.  DISPLAY SHIPS IN PLANETARY ORBITS"
1190  PRINT "6.  DISPLAY STATUS OF YOUR SHIPS"
1200  PRINT "7.  SET A SHIP DESTINATION"
1210  PRINT "8.  PROTEST A CLAIM"
1220  PRINT "9.  ATTEMPT TO CLAIM-JUMP"
1230  PRINT "10. ATTEMPT SABOTAGE"
1240  PRINT "11. FINISHED COMMANDS FOR TODAY"
1250  INPUT "YOUR COMMAND CHOICE";C%: IF C% < 1 OR C% > 11 THEN 1120
1260  ON C% GOTO 1270,1280,1770,1870,1990,1290,2120,1300,1310,1320,1330
1270 S7% = S9%: PRINT "CENTERED WHERE?":P6% = 0: GOSUB 1360:J = P8: GOTO 1570
1280 S7% = S8%: PRINT "CENTERED WHERE?":P6% = 0: GOSUB 1360:J = P8: GOTO 1570
1290 J = P9%: GOTO 1470
1300  GOTO 2360
1310  GOTO 3140
1320  GOTO 2610
1330  RETURN 
1340  rem Select Planet.
1350  PRINT "SELECT FROM THE FOLLOWING PLANETS:"
1360 K = 0: FOR P8 = P6% TO NUM_PLANETS: PRINT  TAB K * 10 + 1);P8;"-";planetName(P8);" ";
1370 K = K + 1: IF K = 4 THEN K = 0: PRINT 
1380  NEXT : IF K > 0 THEN  PRINT 
1381  INPUT "YOUR CHOICE";P8:P8 =  INT (P8)
1390  IF P8 < P6% OR P8 > NUM_PLANETS THEN 1350
1391  RETURN 
1400 : REM  rem 
1410  PRINT "SELECT FROM THE FOLLOWING FAMILIES:"
1420  FOR K = 1 TO NUM_PLAYERS: IF K = P9% THEN 1440
1430  PRINT K;"-";B1$(K - 1)
1440  NEXT 
1450  INPUT "YOUR CHOICE";P5%: IF P5% < 1 OR P5% = P9% OR P5% > NUM_PLAYERS THEN 1410
1460  RETURN 
1465  rem Ship status
1470  PRINT 
1480  PRINT "SHIP STATUS OF ";B1$(J - 1);" FAMILY ON DAY";D0%
1490  PRINT "SHIP (SKILL)"; TAB 15);"FROM"; TAB 23);"TO"; TAB 31);"DAYS TO GO"
1500  PRINT "------------"; TAB 15);"----"; TAB 23);"--"; TAB 31);"----------"
1510  FOR I = 0 TO NUM_SHIPS - 1: IF S0%(J - 1,I) < 0 THEN 1550
1520  PRINT I + 1; TAB 6);"(";S0%(J - 1,I);")"; TAB 15);
1530 P8 =  ABS (S1%(J - 1,I)): PRINT planetName(P8); TAB 23);
1540  PRINT planetName(S2%(J - 1,I)); TAB 31);S3%(J - 1,I)
1550  NEXT 
1560  RETURN 
1570 rem Solar System Map
1580  CLS 
1590  IF S7% = S8% THEN  PRINT "SMALL";
1591  IF S7% <  > S8% THEN  PRINT "LARGE";
1600  PRINT " SOLAR SYSTEM CENTERED ON ";planetName(J)
1610 X1 = orbitRadius(J) *  COS (planetAngle(J)):Y1 = orbitRadius(J) *  SIN (planetAngle(J))
1620  FOR K = 0 TO NUM_PLANETS
1630 X2 = orbitRadius(K) *  COS (planetAngle(K)) - X1:Y2 = orbitRadius(K) *  SIN (planetAngle(K)) - Y1
1640  IF J = K THEN 1660
1650 X3 =  SQR ( SQR (X2 * X2 + Y2 * Y2)):X2 = X2 / X3:Y2 = Y2 / X3
1660 V% =  INT (( - Y2 + .5 * S7%) * M9% / S7%) + 1:H% =  INT ((X2 + .5 * S7%) * M8% / S7%) + 1
1670  IF V% < 1 OR V% > M9% OR H% < 1 OR H% > M8% THEN 1740
1680 P% = (V% - 1) * CHARS_LINE + SCREEN_ADDR + H%:P2% =  PEEK (P%): IF P2% <> 32 THEN 1740 : rem Mapping to screen.
1690  IF K = 0 THEN P2% =  ASC ("*"): ELSE P2% =  ASC ("+")
1691  POKE P%,P2%: FOR P2 = 1 TO 7:H% = H% + 1:P% = P% + 1: IF H% > M8% + 7 THEN 1720
1700 resourceSeed =  PEEK (P%): IF resourceSeed <  > 43 AND resourceSeed <  > 42 THEN 1710: ELSE P2 = 7: GOTO 1720
1710  IF P2 >  LEN (planetName(K)) THEN  POKE P%,32: ELSE  POKE P%, ASC ( MID$ (planetName(K),P2,1))
1720  NEXT 
1740  NEXT 
1750  INPUT "PRESS ENTER";A$
1760  RETURN 
1770 rem Travel Times 
1780  PRINT : PRINT "SOLAR SYSTEM TRAVEL TIMES (DAYS)"
1790  PRINT "FROM/TO";: FOR J = 1 TO NUM_PLANETS
1800  PRINT  TAB 8 + 4 * J);J;: NEXT : PRINT 
1810  PRINT "---------";: FOR J = 1 TO NUM_PLANETS: PRINT  TAB 8 + 4 * J);"===";: NEXT : PRINT 
1820  FOR J = 1 TO NUM_PLANETS: PRINT J;" ";planetName(J);
1830  FOR P8 = 1 TO NUM_PLANETS
1840  GOSUB 4470
1850  PRINT  TAB 8 + 4 * P8);S;
1860  NEXT : PRINT : NEXT : PRINT : RETURN 
1870  PRINT 
1880  PRINT "MINING CONCESSIONS AS OF DAY";D0%
1890  PRINT "PLANET";: FOR J = 0 TO MAX_PLAYER: PRINT  TAB 10 + 11 * J); LEFT$ (B1$(J),9);:MT%(J) = 0
1900  NEXT : PRINT  TAB 10 + 11 * NUM_PLAYERS);"UNCLAIMED"
1920  PRINT "------";: FOR J = 0 TO NUM_PLAYERS: PRINT  TAB 10 + 11 * J);"=========";: NEXT : PRINT 
1950  FOR P = 1 TO NUM_PLANETS: PRINT planetName(P);: FOR J = 1 TO NUM_PLAYERS
1960  PRINT  TAB 12 + 11 * (J - 1));resources(P,J);: NEXT 
1970  PRINT  TAB 12 + 11 * NUM_PLAYERS);resources(P,0): NEXT 
1980  PRINT : RETURN 
1990  PRINT 
2000  PRINT "SHIPS IN PLANETARY ORBIT ON DAY";D0%
2010  PRINT "PLANET";: FOR J = 0 TO NUM_PLAYERS - 1: PRINT  TAB 10 + 11 * J); LEFT$ (B1$(J),9);: NEXT 
2020  PRINT  TAB 10 + 11 * NUM_PLAYERS);"PATROL"
2040  PRINT "------";: FOR J = 0 TO NUM_PLAYERS: PRINT  TAB 10 + 11 * J);"=========";: NEXT : PRINT 
2080  FOR J = 1 TO NUM_PLANETS: PRINT planetName(J);: FOR K = 1 TO NUM_PLAYERS
2090  PRINT  TAB 12 + 11 * (K - 1));: GOSUB 2300: PRINT F%;: NEXT 
2100  PRINT  TAB 12 + 11 * NUM_PLAYERS);: GOSUB 2340: PRINT F%: NEXT 
2110  PRINT : RETURN 
2120 : REM  rem 
2130  GOSUB 1290
2140  INPUT "SHIP NUMBER";P7%: IF P7% < 1 OR P7% > NUM_SHIPS THEN 2140
2150  IF S3%(P9% - 1,P7% - 1) = 0 OR S1%(P9% - 1,P7% - 1) < 0 THEN 2170
2160  PRINT "NOT ALLOWED ON THAT SHIP.": RETURN 
2170 P6% = 1: PRINT "DESTINATION:": GOSUB 1360
2180 J =  ABS (S1%(P9% - 1,P7% - 1))
2190  GOSUB 4470
2200 S3%(P9% - 1,P7% - 1) = S
2210 S1%(P9% - 1,P7% - 1) =  - J:S2%(P9% - 1,P7% - 1) = P8: GOTO 1290
2220 : REM  rem 
2230 F% = 0: FOR L = 0 TO NUM_SHIPS - 1
2240  IF (S1%(K - 1,L) =  - J) OR (S2%(K - 1,L) = J AND S3%(K - 1,L) = 0) THEN F% = F% + S0%(K - 1,L) * S0%(K - 1,L)
2250  NEXT :F% =  SQR (F%): RETURN 
2260 : REM  rem 
2270 F% = 0: FOR L = 0 TO NUM_SHIPS - 1
2280  IF (S1%(K - 1,L) =  - J) OR (S2%(K - 1,L) = J AND S3%(K - 1,L) = 0) THEN F% = L + 1
2290  NEXT : RETURN 
2300 : REM  rem 
2310 F% = 0: FOR L = 0 TO NUM_SHIPS - 1
2320  IF (S1%(K - 1,L) =  - J) OR (S2%(K - 1,L) = J AND S3%(K - 1,L) = 0) THEN F% = F% + 1
2330  NEXT : RETURN 
2340 rem Calc Patrol force. 
2350 F% = 0: FOR L = 0 TO NUM_PATROLS - 1: IF S5%(L) = J AND S6%(L) = 0 THEN F% = F% + 1
2351  NEXT : RETURN 
2360 : REM  rem 
2370  IF P(P9% - 1) = 0 THEN 2420
2380  PRINT "THE POLITICAL CLIMATE ON EARTH IS ";
2390  IF P(P9% - 1) >  = 1 THEN  PRINT "VERY ";
2400  PRINT "INAPPROPRIATE"
2410  PRINT "AT THIS TIME FOR THE ";B1$(P9% - 1);" FAMILY.": GOTO 4460
2420  PRINT "PROTEST A CLAIM AT WHICH PLANET?"
2430 P6% = 1: GOSUB 1360
2440  PRINT "PROTEST A CLAIM OF WHICH FAMILY?"
2450  GOSUB 1420
2460  IF resources(P8,P5%) > 0 THEN 2490
2470  PRINT "THE ";B1$(P5% - 1);" FAMILY HAS NO CONCESSIONS ON ";planetName(P8);"."
2480  RETURN 
2490  PRINT "YOUR PROTEST HAS BEEN LODGED."
2500  PRINT "EARTH'S MINING COUNCIL IS NOW CONSIDERING IT."
2510 K = P9%:J = 3:D =  INT (6 *  RND (0)): GOSUB 2220
2520 D = D + F%:K = P5%: GOSUB 2220
2530  IF F% > 0 THEN  PRINT "THIS ATTEMPT IS BEING FOUGHT VIGOROUSLY"
2540  IF F% > 0 THEN  PRINT "BY REPRESENTATIVES OF THE ";B1$(P5% - 1);" FAMILY."
2550 D = D - F%: IF D < 6 THEN 2580
2555  PRINT "THE COUNCIL FINDS A FLAW IN THE CONCESSION."
2560  PRINT "THE CONCESSION IS MADE PUBLIC AGAIN.":P(P9% - 1) = .5
2570 resources(P8,P5%) = resources(P8,P5%) - 1:resources(P8,0) = resources(P8,0) + 1: GOTO 4460
2580  PRINT "THE COUNCIL FINDS NO FLAW IN THE CONCESSION."
2590 P(P9% - 1) = .5: IF D > 2 THEN P(P9% - 1) = 1
2600  GOTO 2380
2610 : REM  rem 
2620  IF P(P9% - 1) = 0 THEN 2650
2630  PRINT "THIS IS A BAD TIME TO TRY. PEOPLE ARE SUSPICIOUS OF"
2640  PRINT "THE ";B1$(P9% - 1);" FAMILY TODAY.": RETURN 
2650  PRINT "ATTEMPT SABOTAGE IN ORBIT AROUND WHICH PLANET?"
2660 P6% = 1: GOSUB 1360:K = P9%:J = P8: GOSUB 2260: IF F% > 0 THEN 2680
2670  PRINT "YOU HAVE NO SHIPS THERE.": RETURN 
2680  PRINT "ATTEMPT SABOTAGE AGAINST WHICH FAMILY?"
2690  GOSUB 1420
2700 K = P5%:J = P8: GOSUB 2260: IF F% > 0 THEN 2720
2710  PRINT "NO SHIPS OF THE ";B1$(P5% - 1);" FAMILY THERE.": RETURN 
2720 P7% = F%:D =  INT (6 *  RND (0)) +  INT (6 *  RND (0))
2730  PRINT "A SABOTAGE IS ATTEMPTED AGAINST A SHIP OF THE"
2740  PRINT B1$(P5% - 1);" FAMILY IN ORBIT AROUND ";planetName(P8);"."
2750  GOSUB 4460
2760 K = P9%:J = P8: GOSUB 2220:D = D + F%
2770 K = P5%: GOSUB 2220:D = D - F%: GOSUB 2340: IF D < 7 + F% THEN 2820
2780  PRINT "SABOTAGE SUCCEEDS. ";B1$(P5% - 1);" SHIP OUT OF CONTROL AND SPINNING"
2790  PRINT "OUT OF ORBIT AROUND ";planetName(P8);"."
2800 P5% = P5% - 1:P7% = P7% - 1:S3%(P5%,P7%) =  INT (D - 6 + 6 *  RND (0))
2810 P =  ABS (S1%(P5%,P7%)):S1%(P5%,P7%) = 0:S2%(P5%,P7%) = P: GOTO 4460
2820  PRINT "THE ATTEMPTED SABOTAGE FAILS."
2830  IF D < 2 THEN 2870
2840  PRINT "HOWEVER, THE PERPETRATOR ESCAPES LEAVING SUSPICIONS"
2850  PRINT "OF ";B1$(P9% - 1);" TAMPERING, BUT NO PROOF."
2860 P(P9% - 1) = .5: GOSUB 4460: GOTO 2380
2870  PRINT "AN AGENT OF THE ";B1$(P9% - 1);" FAMILY IS CAUGHT."
2880 P(P9% - 1) = P(P9% - 1) + 1:F% = 0: FOR K = 0 TO NUM_PATROLS - 1
2890  IF (S4%(K) =  - P8) OR (S5%(K) = P8 AND S6%(K) = 0) THEN F% = K + 1
2900  NEXT : IF F% = 0 THEN 3010
2910  PRINT "AN EARTH PATROL SHIP WILL ESCORT THE AGENT'S SHIP"
2920  PRINT "BACK TO EARTH WHERE THE AGENT WILL BE ARRAIGNED."
2930 F% = F% - 1:S4%(F%) = P8:S5%(F%) = 3
2940 S =  SQR (orbitRadius(P8) * orbitRadius(P8) + orbitRadius(3) * orbitRadius(3) - 2 * orbitRadius(P8) * orbitRadius(3) *  COS (planetAngle(P8) - planetAngle(3)))
2950 S =  -  INT ( -  SQR (S) * 0.23383161):S6%(F%) = S
2960 K = P9%:J = P8: GOSUB 2260:F% = F% - 1:S1%(P9% - 1,F%) = P8%:S2%(P9% - 1,F%) = 3
2970 D =  INT (6 - D + (6 + 10 * P(P9% - 1)) *  RND (0)):S3%(P9% - 1,F%) = S + D

2980  PRINT B1$(P9% - 1);" LAWYERS ARE CONFIDENT THEY CAN GET THE"
2990  PRINT "AGENT AND SHIP RELEASED";D;"DAYS AFTER ARRIVAL."
3000  GOTO 2380
3010  PRINT "THERE ARE NO EARTH PATROL SHIPS NEAR ";planetName(P8);"."
3020  PRINT "THE ";B1$(P5% - 1);" FAMILY RUTHLESSLY TAKES VENGEANCE INTO THEIR"
3030  PRINT "OWN HANDS BY TORTURING THE AGENT FOR INFORMATION."
3050  PRINT "ONE SHIP BREAKS ORBIT TO INTERROGATE THE PRISONER."
3060 P5% = P5% - 1:P7% = P7% - 1:S0%(P5%,P7%) = S0%(P5%,P7%) + 1
3070 S1%(P5%,P7%) = P8:S2%(P5%,P7%) = P8:S3%(P5%,P7%) = 1
3080  PRINT "THE AGENT IS RETURNED AFTER ONE DAY, BUT"
3090 K = P9%:J = P8: GOSUB 2260:F% = F% - 1
3100 S0%(P9% - 1,F%) = S0%(P9% - 1,F%) - 1:S1%(P9% - 1,F%) = P8
3110 S2%(P9% - 1,F%) = P8:D =  INT (6 - D + 6 *  RND (0)):S3%(P9% - 1,F%) = 1 + D
3120  PRINT "IT WILL BE";D;"MORE DAYS UNTIL HE RECOVERS."
3130  GOTO 2380
3140 : REM  rem 
3150  IF P(P9% - 1) > 0 THEN 2380
3160  PRINT "JUMP A CLAIM AT WHICH PLANET?"
3170 P6% = 1: GOSUB 1360:K = P9%:J = P8: GOSUB 2260: IF F% > 0 THEN 3190
3180  PRINT "YOU HAVE NO SHIPS THERE.": RETURN 
3190  PRINT "ATTEMPT CLAIM-JUMPING AGAINST WHICH FAMILY?"
3200  GOSUB 1420
3210  IF resources(P8,P5%) = 0 THEN 2470
3220  PRINT "ATTEMPTING TO TAMPER WITH THE ";B1$(P5% - 1)
3230  PRINT "MARKER BEACONS ON ";planetName(P8);"..."
3240 D =  INT (6 *  RND (0)) +  INT (6 *  RND (0)):K = P9%:J = P8: GOSUB 2220
3250 D = D + F%:K = P5%:J = P8: GOSUB 2260:P7% = F%: GOSUB 2220:D = D - F%
3260  GOSUB 2340
3270  IF D > 6 + F% THEN 3290
3275  PRINT "ATTEMPTED TAMPERINNG FAILS."
3280  IF P7% + F% > 0 THEN 2830
3285  GOTO 2840
3290  PRINT "THE MARKERS HAVE BEEN SUCCESSFULLY CHANGED.": GOSUB 4460
3300  PRINT "THE ";B1$(P9% - 1);" FAMILY TRIES TO SNEAK A CHANGE"
3310  PRINT "OF OWNERSHIP THROUGH THE MINING COUNCIL."
3320 D =  INT (6 *  RND (0)) +  INT (6 *  RND (0)):K = P9%:J = 3: GOSUB 2220
3330 D = D + F%:K = P5%: GOSUB 2220
3340  IF F% > 0 THEN  PRINT "THIS ATTEMPT IS BEING FOUGHT VIGOROUSLY BY"
3350  IF F% > 0 THEN  PRINT "REPRESENTATIVES OF THE ";B1$(P5% - 1);" FAMILY."
3360 D = D - F%: IF D < 7 THEN 3390
3370  PRINT "THE CONCESSION IS TRANSFERRED.":P(P9% - 1) = .5: GOSUB 4460
3380 resources(P8,P5%) = resources(P8,P5%) - 1:resources(P8,P9%) = resources(P8,P9%) + 1: RETURN 
3390  PRINT "THE DASTARDLY ATTEMPT IS THWARTED BY ADROIT POLITICS."
3400  GOTO 2590
3410 Rem Computer player
3420  FOR P7 = 0 TO NUM_SHIPS - 1: IF S3%(P9% - 1,P7) > 0 OR P7 = H%(P9% - 1) THEN 3580
3430 P8 = S1%(P9% - 1,P7):J = P8: IF resources(P8,0) > 2 *  RND (0) THEN 3580
3440 D =  RND (0): IF Q(0) > 12 *  RND (0) THEN 3480
3450 T% = 0: IF P8 = 3 THEN T% = 10
3460  FOR S = 0 TO NUM_PLAYERS:T% = T% + resources(P8,S): NEXT 
3470  IF T% *  RND (0) > 1 THEN 3580
3471  GOTO 3510
3480  FOR S = 1 TO NUM_PLANETS: IF Q(S) < D THEN 3500
3490 P8 = S:S = NUM_PLANETS
3500  NEXT : GOTO 3550
3510  IF  RND (0) < .25 THEN 3540
3511  FOR S = 1 TO NUM_PLANETS: IF T(S) < D THEN 3530
3520 P8 = S:S = NUM_PLANETS
3530  NEXT : GOTO 3550
3540 P8 = 3
3550 S2%(P9% - 1,P7) = P8:S1%(P9% - 1,P7) =  - J
3560  GOSUB 4470
3570 S3%(P9% - 1,P7) = S
3580  NEXT 
3590 : REM  rem 
3600  FOR J = 1 TO NUM_PLANETS:P8 = J: IF P(P9% - 1) > 0 THEN 3780
3605 K = P9%: GOSUB 2300:resourceSeed = F%
3610  IF F% = 0 THEN 3780
3620  GOSUB 2340:resourceSeed = resourceSeed - F%: FOR K9 = 1 TO NUM_PLAYERS:K = O%(K9 - 1): IF K = P9% THEN 3770
3625 P5% = K
3630  IF P(P9% - 1) > 0 THEN 3770
3640  GOSUB 2300: IF (resourceSeed - F%) * .4 + .1 <  RND (0) THEN 3770
3645 X4% = F%
3660  IF X4% = 0 OR P(P9% - 1) > 0 THEN 3680
3665 X5% = K: GOSUB 2700:K = X5%:P5% = K
3670  IF P(P9% - 1) > 0 THEN 3770
3675 X4% = X4% - 1: GOTO 3660
3680  IF resources(P8,P5%) = 0 THEN 3710
3685 X6% = resources(P8,P5%)
3690 X5% = K: GOSUB 3220:K = X5%: IF P(P9% - 1) > 0 THEN 3770
3700 X6% = X6% - 1: IF X6% > 0 THEN 3690
3710  IF P8 <  > 3 THEN 3770
3720 P8 = 0: FOR X5 = 1 TO NUM_PLANETS: IF resources(X5,P5%) = 0 THEN 3740
3730 P8 = X5:X5 = NUM_PLANETS
3740  NEXT : IF P8 = 0 THEN 3770
3750  GOSUB 4490
3760 X5 = K: GOSUB 2500:K = X5: IF P(P9% - 1) = 0 THEN 3720
3770 P8 = J: NEXT 
3780  NEXT 
3790  IF P(P9% - 1) > 0 OR Q(0) > 12 *  RND (0) THEN  RETURN 
3800 X4% = 0: FOR K9 = 1 TO NUM_PLAYERS:K = O%(K9 - 1):X5% = 0
3810  FOR J = 1 TO NUM_PLANETS:X5% = X5% + resources(J,K): NEXT 
3820  IF X5% < X4% THEN 3830
3825 X4% = X5%:resourceSeed = K
3830  NEXT : IF X4% = 0 OR resourceSeed = P9% THEN  RETURN 
3835 K = resourceSeed
3840 P8 = 0:P5% = K:K = P9%: FOR J = NUM_PLANETS TO 1 STEP  - 1:F% = 0: FOR L = 0 TO NUM_SHIPS - 1: IF S2%(P9% - 1,L) = J THEN F% = 1
3850  NEXT : IF resources(J,P5%) = 0 OR F% = 0 THEN 3870
3860 P8 = J:J = 1
3870  NEXT : IF P8 > 0 THEN 3910
3880  FOR J = 1 TO NUM_PLANETS: IF resources(J,P5%) = 0 THEN 3900
3890 P8 = J:J = NUM_PLANETS
3900  NEXT : IF P8 = 0 THEN  RETURN 
3910  GOSUB 4490: GOSUB 2500
3920  RETURN 
3930 : REM  rem 
3940 : REM  rem 
3950  FOR P7 = 0 TO NUM_PATROLS - 1: IF S6%(P7) > 0 THEN 4110
3960 P8 = S4%(P7):J = P8: IF P8 = 3 OR resources(P8,0) > 0 THEN 4110
3970 D =  RND (0): IF Q(0) > 12 *  RND (0) THEN 4010
3980 T% = 0: IF P8 = 3 THEN T% = 10
3990  FOR S = 0 TO NUM_PLAYERS:T% = T% + resources(P8,S): NEXT 
4000  IF T% *  RND (0) > 1 THEN 4110
4005  GOTO 4040
4010  FOR S = 1 TO NUM_PLANETS: IF Q(S) < D THEN 4030
4020 P8 = S:S = NUM_PLANETS
4030  NEXT : GOTO 4080
4040  IF  RND (0) < .25 THEN 4070
4045  FOR S = 1 TO NUM_PLANETS: IF T(S) < D THEN 4060
4050 P8 = S:S = NUM_PLANETS
4060  NEXT : GOTO 4080
4070 P8 = 3
4080 S5%(P7) = P8
4090 S =  SQR (orbitRadius(P8) * orbitRadius(P8) + orbitRadius(J) * orbitRadius(J) - 2 * orbitRadius(J) * orbitRadius(P8) *  COS (planetAngle(P8) - planetAngle(J)))
4100 S6%(P7) =  -  INT ( -  SQR (S) * 0.1654392)
4110  NEXT 
4120 : REM  rem 
4130  FOR K = 0 TO NUM_PLAYERS - 1: FOR J = 0 TO NUM_SHIPS - 1: IF S3%(K,J) > 0 THEN S3%(K,J) = S3%(K,J) - 1
4140  IF S3%(K,J) > 0 OR  ABS (S1%(K,J)) = S2%(K,J) THEN 4180
4150  PRINT B1$(K);" SHIP ENTERS ORBIT AROUND ";planetName(S2%(K,J));"."
4170  GOSUB 4460:S1%(K,J) = S2%(K,J)
4180 S1%(K,J) =  ABS (S1%(K,J)): NEXT : NEXT 
4190  FOR K = 0 TO NUM_PATROLS - 1: IF S6%(K) > 0 THEN S6%(K) = S6%(K) - 1
4200  IF S6%(K) > 0 THEN 4220
4210 S4%(K) = S5%(K)
4220  NEXT 
4230  FOR L = 0 TO NUM_PLANETS:planetAngle(L) = planetAngle(L) + angularVelocity(L): NEXT 
4240 : REM  rem 
4250  FOR K = 0 TO NUM_PLAYERS - 1:J =  INT ( RND (0) * NUM_PLAYERS):S = O%(K):O%(K) = O%(J):O%(J) = S: NEXT 
4260  FOR K = 0 TO NUM_PLAYERS - 1:J = O%(K) - 1: IF P(J) >  = 1 THEN 4430
4265 P9% = J + 1
4270  FOR L = 0 TO NUM_SHIPS - 1: IF S3%(J,L) > 0 THEN 4420
4280 P8 = S1%(J,L): IF resources(P8,0) = 0 OR P(J) >  = 1 THEN 4420
4290 D =  INT (6 *  RND (0)) +  INT (6 *  RND (0)):resourceSeed = K:K = P9%:X4% = J:J = P8:X5% = L
4300  GOSUB 2220:K = resourceSeed:J = X4%:L = X5%:D = D + F% - 4 * P(J): IF D > 4 THEN 4310
4305  GOTO 4420
4310  PRINT "THE MINING COUNCIL GRANTS A MINING CONCESSION ON"
4320  PRINT planetName(P8);" TO THE ";B1$(J);" FAMILY."
4340  GOSUB 4460
4350 D =  INT (6 *  RND (0)) +  INT (6 *  RND (0)):K = P9%:J = 3
4360  GOSUB 2220:K = resourceSeed:J = X4%:L = X5%:D = D + F%:resources(P8,0) = resources(P8,0) - 1
4370 resources(P8,P9%) = resources(P8,P9%) + 1: IF D > 4 AND P(J) = 0 THEN 4420
4380  PRINT "HOWEVER NO MORE CONCESSIONS WILL BE GRANTED"
4390  PRINT "TO THE ";B1$(J);" FAMILY TODAY."
4400  GOSUB 4460
4410 P(J) = 1
4420  NEXT 
4430  IF P(J) > 0 THEN P(J) = P(J) - 1
4435  IF P(J) < 0 THEN P(J) = 0
4440  NEXT 
4450 D0% = D0% + 1: RETURN 

4455 rem Delay
4460  FOR X9 = 1 TO 1000: NEXT : PRINT : RETURN 

4365 rem Travel time calc From J to P8, result returned in S
4470 S =  SQR (orbitRadius(J) * orbitRadius(J) + orbitRadius(P8) * orbitRadius(P8) - 2 * orbitRadius(P8) * orbitRadius(J) *  COS (planetAngle(J) - planetAngle(P8)))
4480 S =  -  INT ( -  SQR (S) * 0.23383161): RETURN 

4485 rem Lodge a protest
4490  PRINT "THE ";B1$(P9% - 1);" FAMILY IS LODGING A PROTEST"
4500  PRINT "AGAINST A CLAIM OF THE ";B1$(P5% - 1);" FAMILY ON"
4510  PRINT planetName(P8);".": GOTO 4460

4520 rem Game over screen.
4530  PRINT "THE MINING COUNCIL DECLARES ALL CONCESSIONS CLOSED."
4540  PRINT "END OF GAME REPORT:": GOSUB 1870
4550  INPUT "PRESS ENTER";A$
4560  PRINT "FAMILY"; TAB 15);"CONCESSIONS"
4570  FOR P = 0 TO NUM_PLAYERS - 1: PRINT B1$(P);
4580  PRINT  TAB 17);:S = 0: FOR L = 0 TO NUM_PLANETS:S = S + resources(L,P + 1): NEXT 
4590  PRINT S: NEXT 
4600  PRINT : INPUT "ANOTHER GAME (Y/N)";A$: IF A$ = "Y" THEN 540

