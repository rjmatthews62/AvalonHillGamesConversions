10 B = 4:C = 6:P = 3:N = 12
20  DIM T(B),G(B),S(B),P(B),D(B),X(B),Y(B),A(B),F(B),N(B),N$(B)
30  DIM C1(C),C2(C),P1(P),NN$(B)
50  DEF  FN S(II) = S(II) * (F(II) > 0) * (D(II) < P(II)) * (1 - (D(II) / P(II)) ^ 2)
130  DATA 1,7,9,12
140 T2 = 0
150  FOR I = 0 TO P: READ P1(I): NEXT 
170  PRINT " LOAD \n\n\n\n\n\n\n\n\n"; TAB( 2);"** NORTH ATLANTIC CONVOY RAIDER **"
171  PRINT : PRINT  TAB( 10);"COPYRIGHT 1980 BY"
172  PRINT : PRINT  TAB( 13);"AVALON HILL"
173  PRINT  TAB( 9);"MICROCOMPUTER GAMES"
174  FOR I = 0 TO B: READ NN$(I),N$(I),T(I),G(I),S(I),P(I),D(I),F(I): NEXT 
175  FOR I = 1 TO 3000: NEXT 
180  PRINT "\n\n\n\n\n\n\nYOU ARE THE CAPTAIN OF THE ";N$(0);"."
190  PRINT "THERE ARE";B;"BRITISH BATTLESHIPS:"
200  FOR I = 1 TO B: PRINT N$(I);" ("; LEFT$ (N$(I),1);")": NEXT 
210  FOR I = 1 TO 3000: NEXT 
220  DATA BIS,BISMARCK,4,25,31,100,0,7
230  DATA HOOD,HOOD,4,15,32,30,0,7
240  DATA KGV,KING GEORGE V,3,25,28.5,80,0,5
250  DATA POW,PRINCE OF WALES,2,25,28.5,80,0,7
260  DATA ROD,RODNEY,3,30,21,75,0,5
261  DATA REP,REPULSE,3,15,33,25,0,7
262  DATA REN,RENOWN,3,15,33,25,0,7
263  DATA RAM,RAMILLES,4,15,21,55,0,5
300 X(0) = N:Y(0) = 1:A(0) = 3 *  pi  / 4: FOR I = 1 TO B
310 X(I) = 1 + (N - 2) *  RND (0):Y(I) = 2 + (N - 2) *  RND (0)
320 A(I) =  ATN ((X(I) - N) / (Y(I) - 1)): NEXT 
330  FOR I = 0 TO C:C1(I) = 1 + (N - 1) *  RND (0)
340 C2(I) = 1 + (N - 3) *  RND (0): NEXT :V1 = 0:V2 = 0:S0 = 0:Q1 = 0:Q2 = 0:Q4 = 0
350 D0 = 9 +  INT (15 *  RND (0)):T0 =  INT (60 *  RND (0)):T1 = 5 +  INT (3 *  RND (0))
351  GOTO 970
360  PRINT : PRINT  RIGHT$ ( STR$ (10000 + 100 * T1 + T0),4);D0;"MAY 1941"
365  FOR I = 1 TO N:M$ = " . . . . . . . . . . . . . . . . . . . "
366 M$ =  LEFT$ (M$,2 * N + 1)
370  FOR J = 0 TO P: IF P1(J) = I THEN M$ =  LEFT$ (M$,2 * N - 1) + "* "
380  NEXT :I9 = 0: IF  INT (Y(0) + .5) = I THEN  GOSUB 1000
390  IF S0 < 0 THEN 450
400  FOR I9 = 1 TO B: IF  INT (Y(I9) + .5) <  > I THEN 420
410  IF (X(I9) - X(O)) ^ 2 + (Y(I9) - Y(0)) ^ 2 <  = 2 THEN  GOSUB 1000
420  NEXT : FOR I9 = 0 TO C: IF  INT (C1(I9) + .5) <  > I THEN 440
430  IF (C2(I9) - X(0)) ^ 2 + (C1(I9) - Y(0)) ^ 2 <  = 2 THEN  GOSUB 1050
440  NEXT 
450  PRINT M$: NEXT 
470  PRINT :A = A(0) * 180 /  pi :A =  INT (360 - A + .01): PRINT "HEADING: ";
475  PRINT  RIGHT$ ( STR$ (A + 1000),3);"T";
480  PRINT  TAB( 17);"SPEED:"; INT ( FN S(0));"KT"
490  PRINT "DAMAGE:"; INT (100 * D(0) / P(0));"%"; TAB( 17);"TURRETS:";T(0)
500  PRINT "FUEL:"; INT (F(0) * 24);"HRS"; TAB( 17);"WEATHER: ";: IF S0 < 0 THEN  PRINT "FOG"
510  IF S0 >  = 0 THEN  PRINT "CLEAR"
570  INPUT "COMMAND";C$
580  IF C$ = "H" THEN 610
590  IF A9 > .25 THEN 630
591  INPUT "HOW MANY HOURS THIS HEADING";T2
600 T2 =  INT (60 * T2): IF T2 <  = 0 THEN 570
601  GOTO 630
610  INPUT "HEADING";A:A = 360 - A
620 A(0) = A *  pi  / 180: GOTO 590
630  IF S0 < 0 THEN 750
631  FOR K = 1 TO B
640 D8 = Y(K) - Y(0):D9 = X(K) - X(0)
650 B8 =  FN S(0) *  COS (A(0)):B9 =  FN S(0) *  SIN (A(0))
660 D =  SQR (D8 * D8 + D9 * D9):D8 = D8 / D:D9 = D9 / D
670 S8 =  - D9:S9 = D9:B7 = B8 * S8 + B9 * S9
680  IF  ABS (B7) >  =  FN S(K) THEN 730
690 S6 =  SQR ( FN S(K) ^ 2 - B7 * B7):S7 = B7
700 B6 = B8 * D8 + B9 * D9: IF S6 <  = B6 THEN 730
710 A = D / (S6 - B6):I =  INT (Y(0) - A * B8):J =  INT (X(0) - A * B9)
715  IF I < 1 OR I > N OR J < 1 OR J > N THEN 730
720 S9 = S7 * S9 + S6 * D9:S8 = S7 * S8 + S6 * D8: GOTO 740
730 S9 = D9:S8 = D8
740  IF S8 = 0 THEN S8 = 1E - 8
741 A(K) =  ATN (S9 / S8)
745  IF S8 < 0 THEN A(K) = A(K) +  pi 
746  NEXT 
750 T3 = 30 +  INT (30 *  RND (0)): IF T3 > T2 THEN T3 = T2
760 T2 = T2 - T3:T0 = T0 + T3: IF T0 < 60 THEN 790
770 T1 = 1 + T1:T0 = T0 - 60: IF T1 < 24 THEN 790
780 D0 = D0 + 1:T1 = 0
790  FOR K = 0 TO B:B8 =  -  FN S(K) *  SIN (A(K)):B9 =  FN S(K) *  COS (A(K))
800 X(K) = X(K) + B8 * T3 * 1.8E - 4:Y(K) = Y(K) - B9 * T3 * 1.8E - 4
810 F(K) = F(K) - T3 / 1440: IF X(K) < .5 THEN 860
811  IF X(K) > N + .5 THEN 870
820  IF Y(K) < .5 THEN 840
821  IF Y(K) < N + .5 THEN 890
830 Y(K) = N + .49: GOTO 850
840 Y(K) = .51
850 A(K) = 3 *  pi  / 2: IF B8 < 0 THEN A(K) =  pi  / 2
851  GOTO 890
860  IF X(K) >  - 50 THEN X(K) = .51
861  GOTO 880
870  IF K <  > 0 THEN 875
871 L =  - 1
872 L = L + 1: IF P1(L) =  INT (Y(K) + .5) THEN 2000
873  IF L < P THEN 872
875 X(K) = N + .49
880 A(K) = 0: IF B9 < 0 THEN A(K) =  pi 
881  GOTO 820
890  NEXT : IF F(0) <  = 0 THEN 7500
891 Q5 = 0: FOR K = 0 TO C:C2(K) = C2(K) - T3 * 1.8E - 3: NEXT 
900 M =  - 1: FOR K = 1 TO B:D = (X(0) - X(K)) ^ 2 + (Y(0) - Y(K)) ^ 2
910  IF D > 2 THEN 920
911 S0 = S0 + 1: IF S0 < 0 THEN 920
912  PRINT N$(K);: GOSUB 1500
913  IF S0 = 0 OR Q4 = 0 THEN  GOSUB 5000
914 Q4 = 1:Q5 = 1
915  IF D > 1 THEN 920
916 M = M + 1:N(M) = K
920  NEXT : IF Q4 = 1 AND Q5 = 0 THEN  GOSUB 6500
921 Q4 = Q5: IF M >  = 0 AND S0 >  = 1 THEN 3000
922 M =  - 1:Q3 = 0: FOR K = 0 TO C
930 D = (X(0) - C2(K)) ^ 2 + (Y(0) - C1(K)) ^ 2: IF D > 2 THEN 940
935 S0 = S0 + 1: IF S0 < 0 THEN 940
936  PRINT "CONVOY";: GOSUB 1500: IF Q1 = 0 THEN  GOSUB 5000
937 Q1 = 1:Q3 = 1: IF D > 1 THEN 940
938 M = M + 1
940  NEXT : IF Q1 = 1 AND Q3 = 0 THEN  GOSUB 6000
941 Q1 = Q3: IF M >  = 0 AND S0 >  = 1 THEN 4000
942  IF Q2 = 1 THEN  GOSUB 7000
943 Q2 = 0:R =  RND (0): IF R > .1 AND R < .9 THEN 990
950  IF R < .5 THEN 970
951  IF T1 > 20 OR T1 < 4 THEN 990
952 S0 = S0 + 1 - (S0 <  - 2)
955  IF S0 < 0 THEN 990
960  PRINT "AIRCRAFT";: GOSUB 1500: IF S0 = 0 THEN  GOSUB 5000
961  GOTO 990
970 S0 = S0 + (S0 >  - 5): IF S0 >  = 0 THEN 990
980  PRINT N$(0);" ENCOUNTERS DENSE FOG ("; RIGHT$ ( STR$ (10000 + 100 * T1 + T0),4);")."
985  IF S0 =  - 1 THEN  GOSUB 5000
986 Q1 = 0:Q2 = 0:Q4 = 0
990 A9 = .25: GET A$: IF A$ = "" AND T2 > 0 THEN 630
991  GOTO 360
1000 C$ =  LEFT$ (N$(I9),1):X =  INT (X(I9) + .5)
1005  IF X < 1 OR X > N THEN  RETURN 
1010 BI = 0: IF  MID$ (M$,2 * X,1) = "." THEN 1045
1020 BI =  - 1: IF  MID$ (M$,2 * X + BI,1) = " " THEN 1045
1030 BI = 1: IF  MID$ (M$,2 * X + BI,1) = " " THEN 1045
1040  RETURN 
1045 M$ =  MID$ (M$,1,2 * X + BI - 1) + C$ +  MID$ (M$,2 * X + BI + 1): RETURN 
1050 C$ = "C":X =  INT (C2(I9) + .5)
1060  GOTO 1005
1500  PRINT " SPOTS ";N$(0);" ("; RIGHT$ ( STR$ (10000 + 100 * T1 + T0),4);")."
1510  RETURN 
2000  PRINT N$(0);" ENTERS PORT.": RESTORE : READ X,X,X,X,M$,M$,X,X,X,X,X,X
2010 V2 = V2 + 20
2020 A9 = 20 * (.5 - (1 - D(0) / P(0)) * F(0) / X)
2030  IF A9 < 0 THEN V2 = V2 - A9
2031  IF A9 >  = 0 THEN V1 = V1 + A9
2040  GOTO 9000
3000 Q1 = 0:Q2 = 0:T2 = 20 +  INT (20 *  RND (0)): PRINT "SURFACE COMBAT WITH"
3010  FOR I = 0 TO M:K = N(I): PRINT N$(K): NEXT : IF T(0) <  = 0 THEN 3300
3020  INPUT "TARGET SHIP (E TO EVADE)";T$
3021  IF  MID$ (T$,1,1) = "E" THEN 3300
3022  GOTO 3030
3025  IF T(0) <  = 0 THEN 3050
3026  INPUT "TARGET SHIP";T$
3030 TA = 0: FOR I = 0 TO M:K = N(I):C$ =  LEFT$ (N$(K), LEN (T$)): IF T$ = C$ AND TA = 0 THEN TA = K
3031  NEXT 
3032 K = TA: IF K > 0 THEN 3050
3040  PRINT "NO SUCH SHIP.": GOTO 3025
3050 I9 = K:I8 = 0:T = 0
3051 T = T + 1: IF T > T(0) THEN 3115
3052 R =  RND (0) / A9: IF R > 1 THEN 3051
3060  IF R > .25 THEN 3090
3061  GOSUB 3500
3070 D(K) = D(K) + G(0) *  SQR ( RND (0)): IF D(K) < P(K) THEN 3080
3075  GOSUB 3700: GOTO 3115
3080 T(K) = T(K) - 1: GOTO 3051
3090 D(K) = D(K) + G(0) *  SQR ( RND (0)) / 4: GOSUB 3600: IF D(K) < P(K) THEN 3051
3095  GOSUB 3700
3115 I9 = 0:I =  - 1
3116 I = I + 1: IF I > M THEN 3201
3117 K = N(I):I8 = K:T = 0
3118 T = T + 1: IF T > T(K) THEN 3116
3119 R =  RND (0) / A9
3120  IF T1 > 20 OR T1 < 4 THEN R = R * 2
3121  IF R > 1 THEN 3118
3130  IF R > .25 THEN 3160
3131  GOSUB 3500
3140 D(0) = D(0) + G(K) *  SQR ( RND (0)): IF D(0) >  = P(0) THEN 8000
3150 T(0) = T(0) - 1:: IF T(0) < 0 THEN T(0) = 0
3151  GOTO 3180
3160 D(0) = D(0) + G(K) *  SQR ( RND (0)) / 4: GOSUB 3600
3170  IF D(0) > P(0) THEN 8000
3180  GOTO 3118
3201 S0 = (M + 1) * 2:A9 =  SQR (A9): GET A$: IF A$ = "" THEN 630
3202  GOTO 360
3300 D1 = 0: FOR I = 0 TO M:K = N(I):D =  FN S(K): IF D > D1 THEN D1 = D
3301  NEXT 
3310 R =  EXP (( FN S(0) - D1) / 8) *  RND (0): IF R < .5 THEN 3025
3320 S0 =  - (3 + M) * 3:T2 = 0: GOTO 980
3500  PRINT "MAJOR HIT ON ";N$(I9);: GOTO 3610
3600  PRINT "MINOR HIT ON ";N$(I9);
3610  IF M > 0 AND I8 > 0 THEN 3630
3620  PRINT ".": RETURN 
3630  PRINT " (FROM ";NN$(I8);")": RETURN 
3700  PRINT N$(I9);" BLOWS UP AND SINKS.":N(I) = N(M):M = M - 1
3710 X(I9) =  - 99:V1 = V1 + 2 *  SQR (P(K)): IF M < 0 THEN Q4 = 0
3711  RETURN 
4000 Q5 =  INT (T(0) * T3 / 20): PRINT Q5;"SINKINGS IN CONVOY."
4001  IF Q2 = 0 THEN  GOSUB 5000
4002 Q2 = 1
4010 V1 = V1 + Q5 / 10: GOTO 990
5000  FOR L = 0 TO 1800: NEXT : RETURN 
6000  PRINT "CONVOY NO LONGER SIGHTED.": GOTO 5000
6500  PRINT "BATTLESHIP OUT OF CONTACT.": GOTO 5000
7000  PRINT "CONVOY NO LONGER IN GUNNERY RANGE.": GOTO 5000
7500  IF S0 < 0 THEN 7700
7501  IF T1 > 20 OR T1 < 4 THEN 7600
7510  PRINT "WELLINGTON AIRCRAFT BOMB THE ";N$(0);".": GOTO 8000
7600  PRINT "BRITISH SUBMARINE TORPEDOES THE ";N$(0);".": GOTO 8000
7700  PRINT N$(0);" HITS AN ICEBERG."
8000  PRINT N$(0);" ROLLS OVER AND SINKS."
8010 V2 = V2 + 5 *  SQR (P(0))
9000 R = (V1 + .01) / (V2 + .01)
9010  IF R < 1 THEN 9020
9011  PRINT N$(0);: GOTO 9030
9020  PRINT "BRITISH";:R = 1 / R
9030  IF R > 3 THEN 9050
9031  IF R > 1.5 THEN 9040
9032  PRINT " MARGINAL";: GOTO 9060
9040  PRINT " MINOR";: GOTO 9060
9050  PRINT " MAJOR";
9060  PRINT " VICTORY."
9070  PRINT : INPUT "GO AGAIN (Y/N)";A$: RESTORE : IF A$ = "Y" THEN 140
9080  END 
